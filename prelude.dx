
'## Dex prelude

'Runs before every Dex program unless an alternative is provided with `--prelude`.

'Wrappers around primitives

Unit = %UnitType
Type = %TyKind
Effects = %EffKind
Fields = %LabeledRowKind

Int64 = %Int64
Int32 = %Int32
Int8  = %Int8
Float64 = %Float64
Float32 = %Float32

Int = Int32
Float = Float32

def internalCast (b:Type) (x:a) : b = %cast b x

def F64ToF (x : Float64) : Float   = internalCast _ x
def F32ToF (x : Float32) : Float   = internalCast _ x
def FToF64 (x : Float)   : Float64 = internalCast _ x
def FToF32 (x : Float)   : Float32 = internalCast _ x
def I64ToI (x : Int64)   : Int     = internalCast _ x
def I32ToI (x : Int32)   : Int     = internalCast _ x
def I8ToI  (x : Int8 )   : Int     = internalCast _ x
def IToI64 (x : Int)     : Int64   = internalCast _ x
def IToI32 (x : Int)     : Int32   = internalCast _ x
def IToI8  (x : Int)     : Int8    = internalCast _ x
def IToF (x:Int) : Float = internalCast _ x
def FToI (x:Float) : Int = internalCast _ x

interface Add a:Type where
  add : a -> a -> a
  sub : a -> a -> a
  zero : a

def (+)  (d:Add a) ?=> : a -> a -> a = add
def (-)  (d:Add a) ?=> : a -> a -> a = sub

instance float64Add : Add Float64 where
  add = \x:Float64 y:Float64. %fadd x y
  sub = \x:Float64 y:Float64. %fsub x y
  zero = FToF64 0.0

instance float32Add : Add Float32 where
  add = \x:Float32 y:Float32. %fadd x y
  sub = \x:Float32 y:Float32. %fsub x y
  zero = FToF32 0.0

instance int64Add : Add Int64 where
  add = \x:Int64 y:Int64. %iadd x y
  sub = \x:Int64 y:Int64. %isub x y
  zero = IToI64 0

instance int32Add : Add Int32 where
  add = \x:Int32 y:Int32. %iadd x y
  sub = \x:Int32 y:Int32. %isub x y
  zero = IToI32 0

instance int8Add : Add Int8 where
  add = \x:Int8 y:Int8. %iadd x y
  sub = \x:Int8 y:Int8. %isub x y
  zero = IToI8 0

instance unitAdd : Add Unit where
  add = \x y. ()
  sub = \x y. ()
  zero = ()

instance tabAdd : Add a ?=> Add (n=>a) where
  add = \xs ys. for i. xs.i + ys.i
  sub = \xs ys. for i. xs.i - ys.i
  zero = for _. zero

interface Mul a:Type where
  mul : a -> a -> a
  one : a

def (*) (d:Mul a) ?=> : a -> a -> a = mul

instance float64Mul : Mul Float64 where
  mul = \x:Float64 y:Float64. %fmul x y
  one = FToF64 1.0

instance float32Mul : Mul Float32 where
  mul = \x:Float32 y:Float32. %fmul x y
  one = FToF32 1.0

instance int64Mul : Mul Int64 where
  mul = \x:Int64 y:Int64. %imul x y
  one = IToI64 1

instance int32Mul : Mul Int32 where
  mul = \x:Int32 y:Int32. %imul x y
  one = IToI32 1

instance int8Mul : Mul Int8 where
  mul = \x:Int8 y:Int8. %imul x y
  one = IToI8 1

instance unitMul : Mul Unit where
  mul = \x y. ()
  one = ()

data Integral a:Type =
  MkIntegral (a->a->a) (a->a->a) -- idiv, rem

def idiv (d:Integral a) ?=> : a -> a -> a = case d of MkIntegral div _ -> div
def rem  (d:Integral a) ?=> : a -> a -> a = case d of MkIntegral _  rm -> rm

@instance int64Integral : Integral Int64 = MkIntegral (\x:Int64 y:Int64. %idiv x y) (\x y. %irem x y)
@instance int32Integral : Integral Int32 = MkIntegral (\x:Int32 y:Int32. %idiv x y) (\x y. %irem x y)
@instance int8Integral  : Integral Int8  = MkIntegral (\x:Int8  y:Int8.  %idiv x y) (\x y. %irem x y)

data Fractional a:Type =
  MkFractional (a->a->a) -- fdiv

def fdiv (d:Fractional a) ?=> : a -> a -> a = case d of MkFractional div -> div

@instance float64Fractional : Fractional Float64 = MkFractional (\x:Float64 y:Float64. %fdiv x y)
@instance float32Fractional : Fractional Float32 = MkFractional (\x:Float32 y:Float32. %fdiv x y)

'Basic polymorphic functions and types

def (&) (a:Type) (b:Type) : Type = %PairType a b
def (,) (x:a) (y:b) : (a & b) = %pair x y
def fst ((x, _): (a & b)) : a = x
def snd ((_, y): (a & b)) : b = y
def swap (p:(a&b)) : (b&a) = (snd p, fst p)

def (<<<) (f: b -> c) (g: a -> b) : a -> c = \x. f (g x)
def (>>>) (g: a -> b) (f: b -> c) : a -> c = \x. f (g x)

flip : (a -> b -> c) -> (b -> a -> c) = \f x y. f y x
uncurry : (a -> b -> c) -> (a & b) -> c = \f (x,y). f x y
const : a -> b -> a = \x _. x

'Vector spaces

data VSpace a:Type = MkVSpace (Add a) (Float -> a -> a)

@superclass
def addFromVSpace (d:VSpace a) : Add a = case d of MkVSpace addDict _ -> addDict

def (.*)  (d:VSpace a) ?=> : Float -> a -> a = case d of MkVSpace _ scale -> scale
(*.)  : VSpace a ?=> a -> Float -> a = flip (.*)
def (/) (_:VSpace a) ?=> (v:a) (s:Float) : a = (fdiv 1.0 s) .* v
def neg (_:VSpace a) ?=> (v:a) : a = (-1.0) .* v

@instance floatVS : VSpace Float = MkVSpace float32Add (*)
@instance tabVS  : VSpace a ?=> VSpace (n=>a) = MkVSpace tabAdd \s xs. for i. s .* xs.i
@instance unitVS : VSpace Unit = MkVSpace unitAdd \s u. ()

'Bool type

data Bool =
  False
  True

def BToI8 (x : Bool) : Int8 =
  case x of
    False -> (IToI8 0)
    True  -> (IToI8 1)

def I8ToB (x : Int8) : Bool =
  t = True
  f = False
  %select x t f

def (&&) (x:Bool) (y:Bool) : Bool =
  x' = BToI8 x
  y' = BToI8 y
  I8ToB $ %and x' y'

def (||) (x:Bool) (y:Bool) : Bool =
  x' = BToI8 x
  y' = BToI8 y
  I8ToB $ %or x' y'

def not  (x:Bool) : Bool =
  x' = BToI8 x
  I8ToB $ %not x'

'Sum types

data Maybe a:Type =
  Nothing
  Just a

def isNothing (x:Maybe a) : Bool = case x of
  Nothing -> True
  Just _ -> False

data (|) a:Type b:Type =
  Left  a
  Right b

def select (p:Bool) (x:a) (y:a) : a = case p of
  True  -> x
  False -> y

def BToI (x:Bool) : Int  =
  case x of
    False -> 0
    True  -> 1

def BToF (x:Bool) : Float = IToF (BToI x)
def todo (a:Type) ?-> : a = %throwError a
def throw (a:Type) ?-> : a = %throwError a

'Effects

def Ref (r:Type) (a:Type) : Type = %Ref r a
def get  (ref:Ref h s)       : {State h} s    = %get  ref
def (:=) (ref:Ref h s) (x:s) : {State h} Unit = %put  ref x
def ask  (ref:Ref h r)       : {Read  h} r    = %ask  ref
def (+=) (ref:Ref h w) (x:w) : {Accum h} Unit = %tell ref x
def (!)  (ref:Ref h (n=>a)) (i:n) : Ref h a = %indexRef ref i
def fstRef (ref: Ref h (a & b)) : Ref h a = %fstRef ref
def sndRef (ref: Ref h (a & b)) : Ref h b = %sndRef ref

def withReader
      (eff:Effects) ?-> (a:Type) ?-> (r:Type) ?->
      (init:r) (action: (h:Type ?-> Ref h r -> {Read h|eff} a))
      : {|eff} a =
    def explicitAction (h':Type) (ref:Ref h' r) : {Read h'|eff} a = action ref
    %runReader init explicitAction

def withAccum
      (eff:Effects) ?-> (a:Type) ?-> (w:Type) ?->
      (action: (h:Type ?-> Ref h w -> {Accum h|eff} a))
      : {|eff} (a & w) =
    def explicitAction (h':Type) (ref:Ref h' w) : {Accum h'|eff} a = action ref
    %runWriter explicitAction

def withState
      (eff:Effects) ?-> (a:Type) ?-> (s:Type) ?->
      (init:s)
      (action: (h:Type ?-> Ref h s -> {State h |eff} a))
      : {|eff} (a & s) =
    def explicitAction (h':Type) (ref:Ref h' s) : {State h'|eff} a = action ref
    %runState init explicitAction

'Type classes

data Eq  a:Type = MkEq  (a -> a -> Bool)
data Ord a:Type = MkOrd (Eq a) (a -> a -> Bool) (a -> a -> Bool)  -- eq, gt, lt

@superclass
def eqFromOrd (d:Ord a) : Eq a = case d of MkOrd eq _ _ -> eq

def (==) (d:Eq a) ?=> (x:a) (y:a) : Bool = case d of MkEq eq -> eq x y
def (/=) (d:Eq a) ?=> (x:a) (y:a) : Bool = not $ x == y

def (>)  (d:Ord a) ?=> (x:a) (y:a) : Bool = case d of MkOrd _ gt _  -> gt x y
def (<)  (d:Ord a) ?=> (x:a) (y:a) : Bool = case d of MkOrd _ _  lt -> lt x y
def (<=) (d:Ord a) ?=> (x:a) (y:a) : Bool = x<y || x==y
def (>=) (d:Ord a) ?=> (x:a) (y:a) : Bool = x>y || x==y

@instance float64Eq : Eq Float64 = MkEq \x:Float64 y:Float64. I8ToB $ %feq x y
@instance float32Eq : Eq Float32 = MkEq \x:Float32 y:Float32. I8ToB $ %feq x y
@instance int64Eq   : Eq Int64   = MkEq \x:Int64   y:Int64.   I8ToB $ %ieq x y
@instance int32Eq   : Eq Int32   = MkEq \x:Int32   y:Int32.   I8ToB $ %ieq x y
@instance int8Eq    : Eq Int8    = MkEq \x:Int8    y:Int8.    I8ToB $ %ieq x y
@instance unitEq    : Eq Unit    = MkEq \x y. True

@instance float64Ord : Ord Float64 = (MkOrd float64Eq (\x y. I8ToB $ %fgt x y)
                                                      (\x y. I8ToB $ %flt x y))
@instance float32Ord : Ord Float32 = (MkOrd float32Eq (\x y. I8ToB $ %fgt x y)
                                                      (\x y. I8ToB $ %flt x y))
@instance int64Ord   : Ord Int64   = (MkOrd int64Eq   (\x y. I8ToB $ %igt x y)
                                                      (\x y. I8ToB $ %ilt x y))
@instance int32Ord   : Ord Int32   = (MkOrd int32Eq   (\x y. I8ToB $ %igt x y)
                                                      (\x y. I8ToB $ %ilt x y))
@instance int8Ord    : Ord Int8    = (MkOrd int8Eq    (\x y. I8ToB $ %igt x y)
                                                      (\x y. I8ToB $ %ilt x y))
@instance unitOrd    : Ord Unit    = (MkOrd unitEq (\x y. False) (\x y. False))

@instance
def pairEq (eqA: Eq a)?=> (eqB: Eq b)?=> : Eq (a & b) = MkEq $
  \(x1,x2) (y1,y2). x1 == y1 && x2 == y2

@instance
def pairOrd (ordA: Ord a)?=> (ordB: Ord b)?=> : Ord (a & b) =
  pairGt = \(x1,x2) (y1,y2). x1 > y1 || (x1 == y1 && x2 > y2)
  pairLt = \(x1,x2) (y1,y2). x1 < y1 || (x1 == y1 && x2 < y2)
  MkOrd pairEq pairGt pairLt

-- TODO: accumulate using the True/&& monoid
@instance
def tabEq (n:Type) ?-> (eqA: Eq a) ?=> : Eq (n=>a) = MkEq $
  \xs ys.
    numDifferent : Float =
      snd $ withAccum \ref. for i.
        ref += (IToF (BToI (xs.i /= ys.i)))
    numDifferent == 0.0

'Transcencendental functions

data Floating a:Type =
  MkFloating (a->a) (a->a) (a->a) (a->a) (a->a) (a->a) (a->a) (a->a) (a->a) (a->a) (a->a) (a->a) (a->a) (a->a->a) (a->a)

def exp    (d:Floating a) ?=> : a -> a = case d of MkFloating f _ _ _ _ _ _ _ _ _ _ _ _ _ _ -> f
def exp2   (d:Floating a) ?=> : a -> a = case d of MkFloating _ f _ _ _ _ _ _ _ _ _ _ _ _ _ -> f
def log    (d:Floating a) ?=> : a -> a = case d of MkFloating _ _ f _ _ _ _ _ _ _ _ _ _ _ _ -> f
def log2   (d:Floating a) ?=> : a -> a = case d of MkFloating _ _ _ f _ _ _ _ _ _ _ _ _ _ _ -> f
def log10  (d:Floating a) ?=> : a -> a = case d of MkFloating _ _ _ _ f _ _ _ _ _ _ _ _ _ _ -> f
def log1p  (d:Floating a) ?=> : a -> a = case d of MkFloating _ _ _ _ _ f _ _ _ _ _ _ _ _ _ -> f

def sin    (d:Floating a) ?=> : a -> a = case d of MkFloating _ _ _ _ _ _ f _ _ _ _ _ _ _ _ -> f
def cos    (d:Floating a) ?=> : a -> a = case d of MkFloating _ _ _ _ _ _ _ f _ _ _ _ _ _ _ -> f
def tan    (d:Floating a) ?=> : a -> a = case d of MkFloating _ _ _ _ _ _ _ _ f _ _ _ _ _ _ -> f

def floor  (d:Floating a) ?=> : a -> a = case d of MkFloating _ _ _ _ _ _ _ _ _ f _ _ _ _ _ -> f
def ceil   (d:Floating a) ?=> : a -> a = case d of MkFloating _ _ _ _ _ _ _ _ _ _ f _ _ _ _ -> f
def round  (d:Floating a) ?=> : a -> a = case d of MkFloating _ _ _ _ _ _ _ _ _ _ _ f _ _ _ -> f

def sqrt   (d:Floating a) ?=> : a -> a = case d of MkFloating _ _ _ _ _ _ _ _ _ _ _ _ f _ _ -> f
def pow    (d:Floating a) ?=> : a -> a -> a = case d of MkFloating _ _ _ _ _ _ _ _ _ _ _ _ _ f _ -> f

def lgamma (d:Floating a) ?=> : a -> a = case d of MkFloating _ _ _ _ _ _ _ _ _ _ _ _ _ _ f -> f

def lbeta (_ : Add a) ?=> (_ : Floating a) ?=> : a -> a -> a = \x y. lgamma x + lgamma y - lgamma (x + y)

@instance
float64Floating : Floating Float64 =
  (MkFloating (\x:Float64. %exp x) (\x. %exp2 x) (\x. %log x) (\x. %log2 x) (\x. %log10 x) (\x. %log1p x)
              (\x. %sin    x) (\x. %cos   x) (\x. %tan   x)
              (\x. %floor  x) (\x. %ceil  x) (\x. %round x)
              (\x. %sqrt   x) (\x y. %fpow x y)
              (\x. %lgamma x))
@instance
float32Floating : Floating Float32 =
  (MkFloating (\x:Float32. %exp x) (\x. %exp2 x) (\x. %log x) (\x. %log2 x) (\x. %log10 x) (\x. %log1p x)
              (\x. %sin    x) (\x. %cos   x) (\x. %tan   x)
              (\x. %floor  x) (\x. %ceil  x) (\x. %round x)
              (\x. %sqrt   x) (\x y. %fpow x y)
              (\x. %lgamma x))

'Working with index sets

def Range (low:Int) (high:Int) : Type = %IntRange low high
def Fin (n:Int) : Type = Range 0 n
def ordinal (i:a) : Int = %toOrdinal i
def size (n:Type) : Int = %idxSetSize n
def unsafeFromOrdinal (n : Type) (i : Int) : n = %unsafeFromOrdinal n i

def fromOrdinal (n:Type) (i:Int) : n =
  case (0 <= i) && (i < size n) of
    True  -> unsafeFromOrdinal _ i
    False -> throw

def asidx (n:Type) ?-> (i:Int) : n = fromOrdinal n i
def (@) (i:Int) (n:Type) : n = fromOrdinal n i
def iota (n:Type) : n=>Int = for i. ordinal i

-- TODO: we want Eq and Ord for all index sets, not just `Fin n`
@instance
def finEq (n:Int) ?-> : Eq (Fin n) = MkEq \x y. ordinal x == ordinal y

@instance
def finOrd (n:Int) ?-> : Ord (Fin n) =
  MkOrd finEq (\x y. ordinal x > ordinal y) (\x y. ordinal x < ordinal y)

'Misc

pi : Float = 3.141592653589793

def id (x:a) : a = x
def dup (x:a) : (a & a) = (x, x)
def map (f:a->{|eff} b) (xs: n=>a) : {|eff} (n=>b) = for i. f xs.i
def zip (xs:n=>a) (ys:n=>b) : (n=>(a&b)) = for i. (xs.i, ys.i)
def unzip (xys:n=>(a&b)) : (n=>a & n=>b) = (map fst xys, map snd xys)
def fanout (n:Type) (x:a) : n=>a = for i. x
def sq (d:Mul a) ?=> (x:a) : a = x * x
def abs (x:Float) : Float = select (x > 0.0) x (-x)
def mod (x:Int) (y:Int) : Int = rem (y + rem x y) y

def slice (xs:n=>a) (start:Int) (m:Type) : m=>a =
  for i. xs.(fromOrdinal _ (ordinal i + start))

def reindex (ixr: b -> a) (tab: a=>v) : b=>v = for i. tab.(ixr i)

def scan (init:a) (body:n->a->(a&b)) : (a & n=>b) =
  swap $ withState init \s. for i.
    c = get s
    (c', y) = body i c
    s := c'
    y

def fold (init:a) (body:(n->a->a)) : a = fst $ scan init \i x. (body i x, ())
def reduce (identity:a) (binop:(a->a->a)) (xs:n=>a) : a =
  -- `binop` should be a commutative and associative, and form a
  -- commutative monoid with `identity`
  -- TODO: implement with a parallelizable monoid-parameterized writer
  fold identity (\i c. binop c xs.i)

-- TODO: call this `scan` and call the current `scan` something else
def scan' (init:a) (body:n->a->a) : n=>a = snd $ scan init \i x. dup (body i x)
-- TODO: allow tables-via-lambda and get rid of this
def fsum (xs:n->Float) : Float = snd $ withAccum \ref. for i. ref += xs i
def sum  (_: Add v) ?=> (xs:n=>v) : v = reduce zero (+) xs
def prod (_: Mul v) ?=> (xs:n=>v) : v = reduce one  (*) xs
def mean (n:Type) ?-> (xs:n=>Float) : Float = sum xs / IToF (size n)
def std (xs:n=>Float) : Float = sqrt $ mean (map sq xs) - sq (mean xs)
def any (xs:n=>Bool) : Bool = reduce False (||) xs
def all (xs:n=>Bool) : Bool = reduce True  (&&) xs


def applyN (n:Int) (x:a) (f:a -> a) : a =
  snd $ withState x \ref. for _:(Fin n).
    ref := f (get ref)

def linspace (n:Type) (low:Float) (high:Float) : n=>Float =
  dx = (high - low) / IToF (size n)
  for i:n. low + IToF (ordinal i) * dx

def transpose (x:n=>m=>Float) : m=>n=>Float = for i j. x.j.i
def vdot (x:n=>Float) (y:n=>Float) : Float = fsum \i. x.i * y.i

-- matmul. Better symbol to use? `@`?
(**) : (l=>m=>Float) -> (m=>n=>Float) -> (l=>n=>Float) = \x y.
  y' = transpose y
  for i k. fsum \j. x.i.j * y'.k.j

(**.) : (n=>m=>Float) -> (m=>Float) -> (n=>Float) = \mat v. for i. vdot mat.i v
(.**) : (m=>Float) -> (n=>m=>Float) -> (n=>Float) = flip (**.)

def inner (x:n=>Float) (mat:n=>m=>Float) (y:m=>Float) : Float =
  fsum \(i,j). x.i * mat.i.j * y.j

'Functions for working with the pseudorandom number generator

-- TODO: newtype
Key = Int64

def hash (x:Key) (y:Int) : Key =
  y64 = IToI64 y
  %ffi threefry2x32 Int64 x y64
def newKey (x:Int) : Key = hash (IToI64 0) x
def splitKey (k:Key) : (Key & Key) = (hash k 0, hash k 1)
def splitKey3 (k:Key) : (Key & Key & Key) =
  (k1, k') = splitKey k
  (k2, k3) = splitKey k'
  (k1, k2, k3)

def many (f:Key->a) (k:Key) (i:n) : a = f (hash k (ordinal i))
def ixkey (k:Key) (i:n) : Key = hash k (ordinal i)
def ixkey2 (k:Key) (i:n) (j:m) : Key = hash (hash k (ordinal i)) (ordinal j)
def rand (k:Key) : Float = F64ToF $ %ffi randunif Float64 k
def randVec (n:Int) (f: Key -> a) (k: Key) : Fin n => a =
  for i:(Fin n). f (ixkey k i)

def randMat (n:Int) (m:Int) (f: Key -> a) (k: Key) : Fin n => Fin m => a =
  for i j. f (ixkey2 k i j)

def randn (k:Key) : Float =
  (k1, k2) = splitKey k
  u1 = rand k1
  u2 = rand k2
  sqrt ((-2.0) * log u1) * cos (2.0 * pi * u2)

def randIdx (n:Type) ?-> (k:Key) : n =
  unif = rand k
  fromOrdinal n $ FToI $ floor $ unif * IToF (size n)

-- TODO: Make this better...
def randInt (k:Key) : Int = (I64ToI k) `mod` 2147483647

def bern (p:Float) (k:Key) : Bool = rand k < p

def randnVec (n:Type) ?-> (k:Key) : n=>Float =
  for i. randn (ixkey k i)

'min / max etc

def minBy (_:Ord o) ?=> (f:a->o) (x:a) (y:a) : a = select (f x < f y) x y
def maxBy (_:Ord o) ?=> (f:a->o) (x:a) (y:a) : a = select (f x > f y) x y

def min (_:Ord o) ?=> (x1: o) -> (x2: o) : o = minBy id x1 x2
def max (_:Ord o) ?=> (x1: o) -> (x2: o) : o = maxBy id x1 x2

def minimumBy (_:Ord o) ?=> (f:a->o) (xs:n=>a) : a =
  reduce xs.(0@_) (minBy f) xs
def maximumBy (_:Ord o) ?=> (f:a->o) (xs:n=>a) : a =
  reduce xs.(0@_) (maxBy f) xs

def minimum (_:Ord o) ?=> (xs:n=>o) : o = minimumBy id xs
def maximum (_:Ord o) ?=> (xs:n=>o) : o = maximumBy id xs

def argmin (_:Ord o) ?=> (xs:n=>o) : n =
  zeroth = (0@_, xs.(0@_))
  compare = \(idx1, x1) (idx2, x2).
    select (x1 < x2) (idx1, x1) (idx2, x2)
  zipped = for i. (i, xs.i)
  fst $ reduce zeroth compare zipped


'Automatic differentiation

-- TODO: add vector space constraints
def linearize (f:a->b) (x:a) : (b & a --o b) = %linearize f x
def jvp (f:a->b) (x:a) : a --o b = snd (linearize f x)
def transposeLinear (f:a --o b) : b --o a = %linearTranspose f

def vjp (f:a->b) (x:a) : (b & b --o a) =
  (y, df) = linearize f x
  (y, transposeLinear df)

def grad (f:a->Float) (x:a) : a = snd (vjp f x) 1.0

def deriv (f:Float->Float) (x:Float) : Float = jvp f x 1.0

def derivRev (f:Float->Float) (x:Float) : Float = snd (vjp f x) 1.0

data HasAllClose a:Type =
  MkHasAllClose (a -> a -> a -> a -> Bool)

data HasDefaultTolerance a:Type =
  MkHasDefaultTolerance a a

def allCloseWithTolerance (d : HasAllClose a) ?=> : a -> a -> a -> a -> Bool = case d of MkHasAllClose f -> f
def (~~) (_:HasAllClose a) ?=> (d:HasDefaultTolerance a) ?=> : a -> a -> Bool =
  case d of MkHasDefaultTolerance atol rtol -> allCloseWithTolerance atol rtol

@instance allCloseF : HasAllClose Float = MkHasAllClose \atol rtol x y. abs (x - y) <= (atol + rtol * abs y)
@instance defaultToleranceF32 : HasDefaultTolerance Float32 = MkHasDefaultTolerance (FToF32 0.00001   ) (FToF32 0.0001 )
@instance defaultToleranceF64 : HasDefaultTolerance Float64 = MkHasDefaultTolerance (FToF64 0.00000001) (FToF64 0.00001)

def checkDerivBase (f:Float->Float) (x:Float) : Bool =
  eps = 0.01
  ansFwd  = deriv    f x
  ansRev  = derivRev f x
  ansNumeric = (f (x + eps) - f (x - eps)) / (2. * eps)
  ansFwd ~~ ansNumeric && ansRev ~~ ansNumeric

def checkDeriv (f:Float->Float) (x:Float) : Bool =
  checkDerivBase f x && checkDerivBase (deriv f) x

'Control flow

def while
    (eff:Effects) ?->
    (cond: Unit -> {|eff} Bool)
    (body: Unit -> {|eff} Unit)
    : {|eff} Unit =
  cond' : Unit -> {|eff} Int8 = \_. BToI8 $ cond ()
  %while cond' body

'Vector support

-- TODO: Reenable vector suport once fixed-width types are supported.
-- def UNSAFEFromOrdinal (n : Type) (i : Int) : n = %unsafeAsIndex n i
--
-- VectorWidth = 4  -- XXX: Keep this synced with the constant defined in Array.hs
-- VectorFloat  = todo
--
-- def packVector (a : Float) (b : Float) (c : Float) (d : Float) : VectorFloat = %vectorPack a b c d
-- def indexVector (v : VectorFloat) (i : Fin VectorWidth) : Float = %vectorIndex v i
--
-- -- NB: Backends should be smart enough to optimize this to a vector load from v
-- def loadVector (v : (Fin VectorWidth)=>Float) : VectorFloat =
--   idx = Fin VectorWidth
--   (packVector v.(UNSAFEFromOrdinal idx 0)
--               v.(UNSAFEFromOrdinal idx 1)
--               v.(UNSAFEFromOrdinal idx 2)
--               v.(UNSAFEFromOrdinal idx 3))
-- def storeVector (v : VectorFloat) : (Fin VectorWidth)=>Float =
--   idx = Fin VectorWidth
--   [ indexVector v (UNSAFEFromOrdinal idx 0)
--   , indexVector v (UNSAFEFromOrdinal idx 1)
--   , indexVector v (UNSAFEFromOrdinal idx 2)
--   , indexVector v (UNSAFEFromOrdinal idx 3) ]
--
-- def broadcastVector (v : Float) : VectorFloat = packVector v v v v
--
-- @instance vectorFloatAdd : Add VectorFloat =
--   (MkAdd ( \x y. %vfadd x y )
--          ( \x y. %vfsub x y )
--          ( broadcastVector zero ))
-- @instance vectorFloatMul : Mul VectorFloat =
--   MkMul (\x y. %vfmul x y) $ packVector 1.0 1.0 1.0 1.0
-- @instance vectorFloatVSpace : VSpace VectorFloat =
--   MkVSpace vectorFloatAdd \x v. broadcastVector x * v

'Tiling

def Tile (n : Type) (m : Type) : Type = %IndexSlice n m

-- One can think of instances of `Tile n m` as injective functions `m -> n`,
-- with the special property that consecutive elements of m map to consecutive
-- elements of n. In this view (+>) is just function application, while ++>
-- is currying followed by function application. We cannot represent currying
-- in isolation, because `Tile n (Tile u v)` does not make sense, unlike `Tile n (u & v)`.
def (+>) (l : Type) ?-> (t:Tile n l) (i : l) : n = %sliceOffset t i
def (++>) (t : Tile n (u & v)) (i : u) : Tile n v = %sliceCurry t i

def tile  (l : Type) ?->
          (fTile : (t:(Tile n l) -> {|eff} l=>a))
          (fScalar : n -> {|eff} a) : {|eff} n=>a = %tiled fTile fScalar
def tile1 (n : Type) ?-> (l : Type) ?-> (m : Type) ?->
          (fTile : (t:(Tile n l) -> {|eff} m=>l=>a))
          (fScalar : n -> {|eff} m=>a) : {|eff} m=>n=>a = %tiledd fTile fScalar

-- TODO: This should become just `loadVector $ for i. arr.(t +> i)`
--       once we are able to eliminate temporary arrays. Until then, we inline for performance...
--def loadTile (t : Tile n (Fin VectorWidth)) (arr : n=>Float) : VectorFloat =
--  idx = Fin VectorWidth
--  (packVector arr.(t +> UNSAFEFromOrdinal idx 0)
--              arr.(t +> UNSAFEFromOrdinal idx 1)
--              arr.(t +> UNSAFEFromOrdinal idx 2)
--              arr.(t +> UNSAFEFromOrdinal idx 3))

'Numerical utilities

def logsumexp (x: n=>Float) : Float =
  m = maximum x
  m + (log $ sum for i. exp (x.i - m))

def logsoftmax (x: n=>Float) : n=>Float =
  lse = logsumexp x
  for i. x.i - lse

def softmax (x: n=>Float) : n=>Float =
  m = maximum x
  e =  for i. exp (x.i - m)
  s = sum e
  for i. e.i / s

def evalpoly (_:VSpace v) ?=> (coefficients:n=>v) (x:Float) : v =
  -- Evaluate a polynomial at x.  Same as Numpy's polyval.
  fold zero \i c. coefficients.i + x .* c

'Length-erased lists

data List a:Type =
  AsList n:Int foo:(Fin n => a)

def (<>) (x:List a) (y:List a) : List a =
  (AsList nx xs) = x
  (AsList ny ys) = y
  nz = nx + ny
  AsList _ $ for i:(Fin nz).
    i' = ordinal i
    case i' < nx of
      True  -> xs.(fromOrdinal _ i')
      False -> ys.(fromOrdinal _ (i' - nx))

'Isomorphisms

data Iso a:Type b:Type = MkIso { fwd: a -> b & bwd: b -> a }

def appIso (iso: Iso a b) (x:a) : b =
  (MkIso {fwd, bwd}) = iso
  fwd x

def flipIso (iso: Iso a b) : Iso b a =
  (MkIso {fwd, bwd}) = iso
  MkIso {fwd=bwd, bwd=fwd}

def revIso (iso: Iso a b) (x:b) : a = appIso (flipIso iso) x

idIso : Iso a a = MkIso {fwd=id, bwd=id}

def (&>>) (iso1: Iso a b) (iso2: Iso b c) : Iso a c =
  (MkIso {fwd=fwd1, bwd=bwd1}) = iso1
  (MkIso {fwd=fwd2, bwd=bwd2}) = iso2
  MkIso {fwd=(fwd1 >>> fwd2), bwd=(bwd1 <<< bwd2)}

def (<<&) (iso2: Iso b c) (iso1: Iso a b) : Iso a c = iso1 &>> iso2

-- Lens-like accessors
-- (note: #foo is an Iso {foo: a & ...b} (a & {&...b}))
def getAt  (iso: Iso a (b & c)) : a -> b = fst <<< appIso iso
def popAt  (iso: Iso a (b & c)) : a -> c = snd <<< appIso iso
def pushAt (iso: Iso a (b & c)) (x:b) (r:c) : a = revIso iso (x, r)
def setAt  (iso: Iso a (b & c)) (x:b) (r:a) : a =
  pushAt iso x $ popAt iso r

-- Prism-like accessors
-- (note: #?foo is an Iso {foo: a | ...b} (a | {|...b}))
def matchWith (iso: Iso a (b | c)) (x: a) : Maybe b =
  case appIso iso x of
    Left x -> Just x
    Right _ -> Nothing
def buildWith (iso: Iso a (b | c)) (x: b) : a = revIso iso $ Left x

swapPairIso : Iso (a & b) (b & a) =
  MkIso {fwd = \(a, b). (b, a), bwd = \(b, a). (a, b)}

-- Complement the focus of a lens-like isomorphism
exceptLens : Iso a (b & c) -> Iso a (c & b) = \iso. iso &>> swapPairIso

swapEitherIso : Iso (a | b) (b | a) =
  fwd = \x. case x of
    Left l -> Right l
    Right r -> Left r
  bwd = \x. case x of
    Left r -> Right r
    Right l -> Left l
  MkIso {fwd, bwd}

-- Complement the focus of a prism-like isomorphism
exceptPrism : Iso a (b | c) -> Iso a (c | b) = \iso. iso &>> swapEitherIso

-- Use a lens-like iso to split a 1d table into a 2d table
def overLens (iso: Iso a (b & c)) (tab: a=>v) : (b=>c=>v) =
  for i j. tab.(revIso iso (i, j))

-- Zipper isomorphisms to easily specify many record/variant fields:
-- #&foo is an Iso ({&...l} & {foo:a & ...r}) ({foo:a & ...l} & {&...r})
-- #|foo is an Iso ({|...l} | {foo:a | ...r}) ({foo:a | ...l} | {|...r})

-- Convert a record zipper isomorphism to a normal lens-like isomorphism
-- by using splitR &>> iso
splitR : Iso a ({&} & a) = MkIso {fwd=\x. ({}, x), bwd=\({}, x). x}

def overFields (iso: Iso ({&} & a) (b & c)) (tab: a=>v) : b=>c=>v =
  overLens (splitR &>> iso) tab

-- Convert a variant zipper isomorphism to a normal prism-like isomorphism
-- by using splitV &>> iso
splitV : Iso a ({|} | a) =
  MkIso {fwd=\x. Right x, bwd=\v. case v of Right x -> x}

def sliceFields (iso: Iso ({|} | a) (b | c)) (tab: a=>v) : b=>v =
  reindex (buildWith $ splitV &>> iso) tab

Char : Type = %Char
def MkChar (c:Int8) : Char = %MkChar c

String : Type = List Char

CharPtr : Type = %CharPtr

interface Show a:Type where
  show : a -> String

def showFloat' (x:Float) : String =
  (n, ptr) = %ffi showFloat (Int & CharPtr) x
  AsList n $ for i:(Fin n).
    MkChar $ %ptrLoad (%ptrOffset ptr (ordinal i))

instance showFloat : Show Float where
  show = showFloat'
