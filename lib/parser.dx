

'Utilities unrelated to parsing

def fromOrdinalExc (n:Type) (i:Int) : {Except} n =
  if (0 <= i) && (i < size n)
    then unsafeFromOrdinal _ i
    else throw ()

-- TODO: allow this to happen in-place
-- TODO: if it takes too long to make that possible, start with a bounded version
def push (ref:Ref h (List a)) (x:a) : {State h} Unit =
  l = get ref
  ref := l <> AsList _ [x]

def indexList (l:List a) (i:Int) : {Except} a =
  (AsList n xs) = l
  xs.(fromOrdinalExc _ i)

'The Parser type

def ParserHandle (h:Type) : Type = (String & Ref h Int)

data Parser a:Type =
  MkParser (h:Type ?-> ParserHandle h -> {Except, State h} a)

def parse (handle:ParserHandle h) (parser:Parser a) : {Except, State h} a =
  (MkParser f) = parser
  f handle

def runParserPartial (s:String) (parser:Parser a) : Maybe a =
  (MkParser f) = parser
  withState 0 \pos.
    catch $ do
      f (s, pos)

'Primitive combinators

def pChar (c:Char) : Parser Unit = MkParser \(s, posRef).
  i = get posRef
  c' = indexList s i
  assert (c == c')
  posRef := i + 1

def pEOF : Parser Unit = MkParser \(s, posRef).
  assert $ get posRef >= listLength s

def (<|>) (p1:Parser a) (p2:Parser a) : Parser a = MkParser \h.
  (s, posRef) = h
  curPos = get posRef
  case catch do parse h p1 of
    Nothing ->
      assert $ curPos == get posRef
      parse h p2
    Just ans -> ans

def return (x:a) : Parser a = MkParser \_. x

def runParser (s:String) (parser:Parser a) : Maybe a =
  runParserPartial s $ MkParser \h.
    ans = parse h parser
    _   = parse h pEOF
    ans

def parseAny : Parser Char = MkParser \h.
  (s, posRef) = h
  i = get posRef
  c' = indexList s i
  posRef := i + 1
  c'

def try (parser:Parser a) : Parser a = MkParser \h.
  (s, posRef) = h
  savedPos = get posRef
  ans = catch do parse h parser
  case ans of
    Nothing ->
      posRef := savedPos
      throw ()
    Just x -> x

'Derived combinators

def parseDigit : Parser Int = try $ MkParser \h.
  c = parse h $ parseAny
  i = W8ToI c - 48
  assert $ 0 <= i && i < 10
  i

def optional (p:Parser a) : Parser (Maybe a) =
  (MkParser \h. Just (parse h p)) <|> return Nothing

def parseMany (parser:Parser a) : Parser (List a) = MkParser \h.
  yieldState (AsList _ []) \results.
    iter \_.
      maybeVal = parse h $ optional parser
      case maybeVal of
        Nothing -> Done ()
        Just x ->
          push results x
          Continue

def parseUnsignedInt : Parser Int = MkParser \h.
  (AsList _ digits) = parse h $ parseMany parseDigit
  yieldState 0 \ref.
    for i. ref := 10 * get ref + digits.i

def parseInt : Parser Int = MkParser \h.
  negSign = parse h $ optional $ pChar '-'
  x       = parse h $ parseUnsignedInt
  case negSign of
    Nothing -> x
    Just () -> (-1) * x

def bracketed (l:Parser Unit) (r:Parser Unit) (body:Parser a) : Parser a =
  MkParser \h.
    _   = parse h l
    ans = parse h body
    _   = parse h r
    ans

def parens (parser:Parser a) : Parser a =
  bracketed (pChar '(') (pChar ')') parser
