
-- TODO: use prelude sum instead once we can differentiate state effect
def sum'  (xs:n=>Float) : Float = snd $ withAccum \ref. for i. ref += xs.i

:p
   f : Float -> Float = \x. x
   jvp f 3.0 2.0
> 2.0

:p
   f = \x. x * x
   jvp f 3.0 1.5
> 9.0

:p
   f = \x. x + x
   jvp f 3.0 2.0
> 4.0

:p
   f = \x. x * x * x
   jvp f 2.0 1.5
> 18.0

:p
   f : Float --o Float = \x. x
   transposeLinear f 2.0
> 2.0

:p
   f : Float --o Float = \x. x + x
   transposeLinear f 1.0
> 2.0

:p
   f : Float --o Float = \x. x + (x + x) * 2.0
   transposeLinear f 1.0
> 5.0

:p
   f : Float --o Float = \x. x * 2.0
   transposeLinear f 1.0
> 2.0

:p
   f : Float --o Float = \x. 2.0 * x
   transposeLinear f 1.0
> 2.0

:p grad (\x. x * x) 1.0
> 2.0

:p deriv (\x. 3.0 / x) 2.0
> -0.75

:p deriv (\x. x / 2.0) 3.0
> 0.5

:p
  f : n:Type ?-> n=>Float -> n=>Float =
   \xs. for i. xs.i * xs.i

  jvp f [1.,2.] [3.,4.]
> [6.0, 16.0]

:p jvp transpose [[1.,2.], [3.,4.]] [[10.,20.], [30.,40.]]
> [[10.0, 30.0], [20.0, 40.0]]

:p jvp sum' [1., 2.] [10.0, 20.0]
> 30.0

f : Float -> Float = \x. snd $ withAccum \ref. ref += x
:p jvp f 1.0 1.0
> 1.0

:p
   f = \x. x * x * x
   jvp (\x. jvp f x 1.0) 2.0 1.0
> 12.0

:p
   f = \x. 4.0 * x * x * x
   deriv (deriv (deriv f)) 1.234
> 24.0

:p
   f : Float --o (Float & Float) = \x. (x, 2.0 * x)
   transposeLinear f (1.0, 3.0)
> 7.0

:p
   f : (Float & Float) --o Float = \(x,y). x + 2.0 * y
   transposeLinear f 1.0
> (1.0, 2.0)

:p deriv cos 0.0
> 0.0

:p deriv sin 0.0
> 1.0

:p (sin 1.0, deriv (deriv sin) 1.0)
> (0.84147096, -0.84147096)

:p (cos 1.0, deriv (deriv (deriv sin)) 1.0)
> (0.5403023, -0.5403023)

:p checkDeriv sin 1.0
> True

:p checkDeriv cos 1.0
> True

-- :p checkDeriv exp 2.0
-- > True

-- :p checkDeriv log 2.0
-- > True

-- :p checkDeriv sqrt 2.0
-- > True

-- :p checkDeriv (\x. cos (sin (exp x))) 2.0
-- > True

:p checkDeriv (deriv sin) 1.0
> True

:p checkDeriv (deriv cos) 1.0
> True

-- badDerivFun : Float -> Float
-- badDerivFun x = x

-- badDerivFun#lin : Float -> (Float, Float --o Float)
-- badDerivFun#lin x = (x, llam t. 2. * t)

-- :p checkDeriv badDerivFun 1.0
-- > False

-- Perturbation confusion test suggested by Barak Pearlmutter
-- https://github.com/HIPS/autograd/issues/4
:p deriv (\x. x * deriv (\y. x * y) 2.0) 1.0
> 2.0

tripleit : Float --o Float = \x. x + x + x

:p tripleit 1.0
> 3.0

:p transposeLinear tripleit 1.0
> 3.0

:p transposeLinear (transposeLinear tripleit) 1.0
> 3.0

:p
  f : n:Type ?->  Float --o n=>Float = \x. for i. x

  transposeLinear f [1.0, 2.0]
> 3.0

:p
  f : n:Type ?-> n=>Float --o n=>Float = \x. for i. x.i * 2.0

  transposeLinear f [1.0, 2.0]
> [2.0, 4.0]

myOtherSquare : Float -> Float =
  \x. snd $ withAccum \w. w += x * x

:p checkDeriv myOtherSquare 3.0
> True

:p
  f : Float -> Float =
    \x. fst (x * x, 2 + 1)

  jvp f 1.0 3.0
> 6.0

:p
  f : Float -> Float = \x.
    x * IToF (1 + 1)

  jvp f 1.0 2.0
> 4.0

:p
  f : (Fin 2)=>Float -> Float =
    \xs. xs.(0 @ Fin 2) * xs.(1 @ Fin 2)

  jvp f [1., 2.] [3.0, 4.0]
> 10.0

:p
  f : (Float & Float) -> Float =
    \(x,y). x * y

  jvp f (1., 2.) (3.0, 4.0)
> 10.0

:p
  f : n:Type ?-> n=>Float -> n=>Float =
    \xs. for i. xs.i * xs.i

  jvp f [1.,2.] [3.,4.]
> [6.0, 16.0]

:p jvp sum' [1., 2.] [3.0, 4.0]
> 7.0

:p grad sum' [1.,2.]
> [1.0, 1.0]


vec = [1.]

:p jvp (\x. vec) [1.] [1.]
> [0.0]

:p grad (\(x, y). vdot x y) ([1.,2.], [3.,4.])
> ([3.0, 4.0], [1.0, 2.0])

:p
  f : Float -> Float = \x.
    y = x * 2.0
    snd $ withAccum \a.
      a += x * 2.0
      a += y
  grad f 1.0
> 4.0

:p
  f : Float -> Float = \x.
    x2 = x * x
    withReader x \xr.
      withReader x2 \x2r.
        5.0 * (ask x2r) + 4.0 * (ask xr) + 2.0
  checkDeriv f 2.0
> True

:p
  f : Float -> Float = \x.
    snd $ withState x \xr.
      for i:(Fin 2).
        xr := get xr * get xr
  checkDeriv f 2.0
> True

:p
  f = \rec.
    ({x=x, y=y, z=z}) = rec
    x * y * IToF z
  (checkDeriv (\x. f {x=x, y=4.0, z=5}) 2.0, checkDeriv (\y. f {x=2.0, y=y, z=5}) 4.0)
> (True, True)

:p
  f = \x.
    y = for i:(Fin 10). { x=x * (IToF $ ordinal i) }
    z = for i.
      ({ x=x, ... }) = y.i
      x
    sum' z
  checkDeriv f 1.0
> True

:p
  f = \x. for i:(Fin 4). { x=x * x * (IToF $ ordinal i) }
  jvp f 2.0 1.0
> [{x = 0.0}, {x = 4.0}, {x = 8.0}, {x = 12.0}]

:p
  s : { a : Float | b : Float } = case 2 == 2 of
    False -> {| a=2.0 |}
    True -> {| b=5.0 |}
  f = \x.
    case s of
      {| a=a |} -> 2.0 * (a + x)
      {| b=b |} -> b * x
  (y, df) = linearize f 2.0
  checkDerivBase f 2.0
> True
