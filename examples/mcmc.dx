
-- === General MCMC utilities ===

LogProb : Type = Float

def runChain
      (initialize: Key -> a)
      (step: Key -> a -> a)
      (numSamples: Int)
      (k:Key)
      : Fin numSamples => a =
  (k1, k2) = splitKey k
  fst $ withState (initialize k1) \s.
    for i:(Fin numSamples).
      x = step (ixkey k2 i) (get s)
      s := x
      x

def propose
      (logDensity : a -> LogProb)
      (cur      : a)
      (proposal : a)
      (k : Key)
      : a =
  acceptProb = exp (logDensity proposal) / exp (logDensity cur)
  select (bern acceptProb k) proposal cur

def meanAndCovariance (n:Type) ?-> (d:Type) ?->
      (xs:n=>d=>Float) : (d=>Float & d=>d=>Float) =
   xsMean :    d=>Float = (for i. sum for j. xs.j.i) / IToF (size n)
   xsCov  : d=>d=>Float = (for i i'. sum for j.
                           (xs.j.i' - xsMean.i') *
                           (xs.j.i  - xsMean.i )   ) / IToF (size n - 1)
   (xsMean, xsCov)

-- === Metropolis-Hastings implementation ===

MHParams : Type = Float  -- step size

def mhStep
      (stepSize: MHParams)
      (logProb: (d=>Float) -> LogProb)
      (k:Key)
      (x:d=>Float)
      : d=>Float =
  (k1, k2) = splitKey k
  proposal = x + stepSize .* randnVec k1
  propose logProb x proposal k2

-- === HMC implementation ===

HMCParams : Type = (Int & Float)  -- leapfrog steps, step size

def leapfrogIntegrate
      (_:VSpace a) ?=>
      ((nsteps, dt): HMCParams)
      (logProb: a -> LogProb)
      ((x, p): (a & a))
      : (a & a) =
  x = x + (0.5 * dt) .* p
  (x, p) = applyN nsteps (x, p) \(xOld, pOld).
    pNew = pOld + dt .* grad logProb xOld
    xNew = xOld + dt .* pNew
    (xNew, pNew)
  p = p + (0.5 * dt) .* grad logProb x
  (x, p)

def hmcStep
      (params: HMCParams)
      (logProb: (d=>Float) -> LogProb)
      (k:Key)
      (x:d=>Float)
      : d=>Float =
  hamiltonian = \(x, p). logProb x - 0.5 * vdot p p
  (k1, k2) = splitKey k
  p = randnVec k1
  proposal = leapfrogIntegrate params logProb (x, p)
  fst $ propose hamiltonian (x, p) proposal k2

-- === test it out ===

def myLogProb (x:(Fin 2)=>Float) : LogProb =
  x' = x - [1.5, 2.5]
  neg $ 0.5 * inner x' [[1.,0.],[0.,20.]] x'

hmcParams = (10, 0.1)
mhParams = 0.1
numSamples = 500
k0 = newKey 1

hmcSamples = runChain randnVec (hmcStep hmcParams myLogProb) numSamples k0
mhSamples  = runChain randnVec (mhStep  mhParams  myLogProb) numSamples k0

:p meanAndCovariance hmcSamples
> ( [1.4468338, 2.4944727]
> , [[1.0656759, 2.0475952e-2], [2.0475952e-2, 5.288498e-2]] )

:plot for i. (IToF (ordinal i), hmcSamples.i.(0@_))
> <graphical output>

:p meanAndCovariance mhSamples
> ([0.6455552, 2.4140577], [[0.38236213, 0.1794126], [0.1794126, 0.228957]])

:plot for i. (IToF (ordinal i), mhSamples.i.(0@_))
> <graphical output>
