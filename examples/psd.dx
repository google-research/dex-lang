' ### PSD solver based on Cholesky decomposition

import linalg

def psdsolve {n} (mat:n=>n=>Float) (b:n=>Float) : n=>Float =
  l = chol mat
  b' = forward_substitute l b
  u = transpose_lower_to_upper l
  backward_substitute u b'

' Test

N = Fin 4
[k1, k2] = split_key $ new_key 0

psd : N=>N=>Float =
  a = for i:N j:N. randn $ ixkey2 k1 i j
  x = a ** transpose a
  x + eye

def padLowerTriMat {n v} [Add v] (mat:LowerTriMat n v) : n=>n=>v =
  for i j.
    if (ordinal j)<=(ordinal i)
      then mat.i.(unsafe_cast j)
      else zero

l = chol psd
l_full = padLowerTriMat l
:p l_full
> [ [3.220772, 0., 0., 0.]
> , [-0.462207, 1.043325, 0., 0.]
> , [0.176507, -0.273588, 1.709453, 0.]
> , [-0.260148, 0.120994, 0.244608, 1.52529] ]

psdReconstructed = l_full ** transpose l_full

:p sum for (i, j). sq (psd.i.j - psdReconstructed.i.j)
> 0.

vec : N=>Float = arb k2

:p (vec, psd **. psdsolve psd vec)
> ( [-1.035494, -1.713369, 1.600502, 0.975181]
> , [-1.035494, -1.713369, 1.600502, 0.975181] )
