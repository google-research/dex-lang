
'Basics:
Syntax for records, variants, and their types.

'Record types

:p {&}
> { &}

:p {a:Int & b:Real}
> {a: Int & b: Real}

:p {a:Int & b:Real &}
> {a: Int & b: Real}

:p {a:Int & a:Real}
> {a: Int & a: Real}


'Records

:p {}
> {,}
:t {}
> { &}

:p {,}
> {,}
:t {,}
> { &}

:p {a=3, b=4}
> {a = 3, b = 4}
:t {a=3, b=4}
> {a: Int & b: Int}

:p {a=3, b=4,}
> {a = 3, b = 4}
:t {a=3, b=4,}
> {a: Int & b: Int}

:p {a=3, a=4}
> {a = 3, a = 4}
:t {a=3, a=4}
> {a: Int & a: Int}


'Variant (enum) types

:p {|}
> { |}

:p {a:Int | b:Real}
> {a: Int | b: Real}

:p {a:Int | b:Real |}
> {a: Int | b: Real}

:p {a:Int | a:Real}
> {a: Int | a: Real}


'Variants (enums)
:p {| a=3 |}

:p
  x : {a:Int | b:Real} = {| a=3 |}
  x
> {| a = 3 |}

:p
  x : {a:Int | a:Real | a:Int} = {| a#1 = 3.0 |}
  x
> {| a#1 = 3.0 |}

:t
  x : {a:Int | a:Real | a:Int} = {| a#1 = 3.0 |}
  x
> {a: Int | a: Real | a: Int}


'Parse errors

:p {a:Int & b:Real | c:Int }

> Parse error:84:20:
>    |
> 84 | :p {a:Int & b:Real | c:Int }
>    |                    ^
> unexpected '|'
> expecting "..", "..<", expression, or negation
:p {a:Int & b:Real & ...c}

> Parse error:92:26:
>    |
> 92 | :p {a:Int & b:Real & ...c}
>    |                          ^
> Extensible records and variants not implemented
:p {&...c}

> Parse error:99:10:
>    |
> 99 | :p {&...c}
>    |          ^
> Extensible records and variants not implemented
:p {a=3, b=4, ...c}

> Parse error:106:19:
>     |
> 106 | :p {a=3, b=4, ...c}
>     |                   ^
> Extensible records and variants not implemented
:p {a:Int | b:Real | ...c}

> Parse error:113:26:
>     |
> 113 | :p {a:Int | b:Real | ...c}
>     |                          ^
> Extensible records and variants not implemented
:p {|...c}

> Parse error:120:10:
>     |
> 120 | :p {|...c}
>     |          ^
> Extensible records and variants not implemented

'Unpacking


:p
  ({b=b, a=a1, a=a2}) = {a=1, b=2, a=3}
  (a1, a2, b)
> (1, (3, 2))

({b=b, a=a1, a=a2}) = {a=1, b=2, a=3}
:p (a1, a2, b)
> (1, (3, 2))

:p
  ({b=b, a=a1, a=a2}) = {a=1, b=2}
  (a1, a2, b)
> Type error:Labels in record pattern do not match record type. Expected structure {a: Int & b: Int}
>
>   ({b=b, a=a1, a=a2}) = {a=1, b=2}
>    ^^^^^^^^^^^^^^^^^

:p
  x : {a:Int | a:Real | a:Int} = {| a#1 = 3.0 |}
  ({| a#1 = a |}) = x
  x
> Type error:Variant not allowed in can't-fail pattern
>
>   ({| a#1 = a |}) = x
>    ^^^^^^^^^^^^^

x : {a:Int | a:Real | a:Int} = {| a#1 = 3.0 |}
({| a#1 = a |}) = x
> Type error:Variant not allowed in can't-fail pattern


'Pattern matching

-- Not allowed: use a let binding instead
:p case {a=1, b=2, a=3} of
  {b=b, a=a1, a=a2} -> (a1, a2, b)
> Type error:Case patterns must start with a data constructor or variant pattern
>
>   {b=b, a=a1, a=a2} -> (a1, a2, b)
>   ^^^^^^^^^^^^^^^^^^

:p
  x : {a:Int | a:Real | b:Int} = {| a#1 = 3.0 |}
  case x of
    {| a#0 = x |} -> i2r x
    {| a#1 = x |} -> x
    {| b = x |} -> i2r x
> 3.0

'Table values and imp lowering

myRecordTable : (Fin 2)=>{a:Int & b:Real} =
  [{a=1, b=2.0}, {a=3, b=4.0}]

:p myRecordTable
> [{a = 1, b = 2.0}, {a = 3, b = 4.0}]

:p for i:(Fin 2).
    ({a=a, b=b}) = myRecordTable.i
    {a=b, b=a}
> [{a = 2.0, b = 1}, {a = 4.0, b = 3}]

myVariantTable = [{| a=1 |}, {| b=2.0 |}]: (Fin 2)=>{a:Int | b:Real}
:p myVariantTable
> [{| a = 1 |}, {| b = 2.0 |}]
 
:p for i:(Fin 2).
    case myVariantTable.i of
      {| a=a |} -> {| b=a |}:{a:Real | b:Int}
      {| b=b |} -> {| a=b |}:{a:Real | b:Int}
> [{| b = 1 |}, {| a = 2.0 |}]
