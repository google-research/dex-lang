:p 1.0 + 2.0
> 3.0

:p
   double = \x. x * 2.0
   double 10.0
> 20.0

:p isum (iota (Fin 10))
> 45

:p
   x = iota (Fin 10)
   isum (for i. x.i)
> 45

:p
   vdot' : n:Type ?-> n=>Real -> n=>Real -> Real =
     \x y. sum (for i. x.i * y.i * 2.0)
   x = iota (Fin 3)
   y = map i2r x
   vdot' y y
> 10.0

:p
   x = iota $ Fin 3
   y = iota $ Fin 4
   z = for i j. i2r x.i * i2r y.j
   sum (for i. sum z.i)
> 18.0

-- :p randint (hash 0 0) 10
-- :p let x = unpack range 10000
--        key = hash 0
--    in sum (for i: randint (key x.i) 10)
NArr = Fin 7

arr = iota NArr

:p for i:NArr. iadd (iota _).i (iota _).i
> [0, 2, 4, 6, 8, 10, 12]

:p isum for i:NArr. 1
> 7

fun = \y. sum (map i2r arr) + y

:p fun 3.0
> 24.0

:p arr
> [0, 1, 2, 3, 4, 5, 6]

:p fold 0 \i. iadd arr.i
> 21

:p 6.0 - 10.0
> -4.0

:p (\(x, y). x + y) (1.0, 2.0)
> 3.0

:p
   f : a:Type ?-> b:Type ?-> (a -> b & a) -> b =
     \(x, y). x y
   f (\x. x, 1)
> 1

:p
   (x,y) = ((1.0,2.0),3.0)
   (x1, x2) = x
   x1 + x2 + y
> 6.0

:p
   x = (1.0,2.0)
   (y,z) = x
   y + z
> 3.0

-- :p let f (x, y) = x + 2 * y;
--        z.i = (x.i, x.i * x.i)
--    in sum (for i. f z.i)
:p exp 1.0
> 2.7182817

:p log 1.0
> 0.0

:p sqrt 2.0
> 1.4142135

:p sin 3.14159
> 2.6535897e-6

:p cos 0.0
> 1.0

:p tan 1.57079
> 158057.9

s = 1.0

:p s
> 1.0

:p [2, 4, 8]
> [2, 4, 8]

:p
   xs = [2,1,0]
   for i. iadd xs.i 10
> [12, 11, 10]

cumsumplus : n=>Real -> n=>Real =
 \xs. snd $ scan 0.0 \i c.
   ans = c + xs.i
   (ans, 1.0 + ans)

:p cumsumplus [1.0, 2.0, 3.0]
> [2.0, 4.0, 7.0]

:p [False, False, True]
> [False, False, True]

:p [False, True, True, False]  -- testing again because it's been flaky
> [False, True, True, False]

:p (True, False)
> (True, False)

:p 1.0 > 2.0
> False

:p 1.0 > neg 2.0
> True

:p 1.0 < 2.0
> True

:p b2i (1.0 < 2.0)
> 1

:p [ 1 < 2, 1.0 < 2.0,  2 < 2, 2.0 < 2.0, 2 < 1, 2.0 < 1.0]
> [True, True, False, False, False, False]

:p [ 1 > 2, 1.0 > 2.0,  2 > 2, 2.0 > 2.0, 2 > 1, 2.0 > 1.0]
> [False, False, False, False, True, True]

:p [ 1 <= 2, 1.0 <= 2.0,  2 <= 2, 2.0 <= 2.0, 2 < 1, 2.0 < 1.0]
> [True, True, True, True, False, False]

:p [ 1 >= 2, 1.0 >= 2.0,  2 >= 2, 2.0 >= 2.0, 2 >= 1, 2.0 >= 1.0]
> [False, False, True, True, True, True]

N3 = Fin 3
N4 = Fin 4

mat = for i:N3 j:N4. iadd (iota _).i (imul 10 (iota _).j)

-- :p isum for (i,j). mat.i.j
-- > 192

litArr = [10, 5, 3]

:p mod 5 3
> 2

:p size (Fin 3)
> 3

:p for i:(Fin 3). 1
> [1, 1, 1]

:p
   (k, _) = (iadd 1 2, 3)
   for i:(Fin 6). iadd k (ordinal i)
> [3, 4, 5, 6, 7, 8]

:p
   k = newKey 0
   mean for i:(Fin 100). randn (ixkey k i)
> -0.115799494

:p
   k = newKey 0
   mean for i:(Fin 100). sq $ randn (ixkey k i)
> 1.2581897

:p for i:(Fin 3) j:(Fin 2). rand $ ixkey2 (newKey 11) i j
> [[0.47415292, 0.9145164], [0.7944602, 0.27679908], [0.58958626, 0.7116251]]

:p
  x = for i:(Fin 3). 0
  x
> [0, 0, 0]

:p
   x = for i:(Fin 3). for j:(Fin 4). 0
   x
> [[0, 0, 0, 0], [0, 0, 0, 0], [0, 0, 0, 0]]

:p fold (for i:(Fin 3). 0.0) $ \i:(Fin 2) c. (for j. c.j + i2r (ordinal j))
> [0.0, 2.0, 4.0]

:p
   mat2 = for i:(Fin 4) j:(Fin 4) . ordinal i
   tmp = for i. mat2.i.i
   tmp
> [0, 1, 2, 3]

:p
  x = [[0, 1], [2, 3]]
  xt = for i j. x.j.i
  xt
> [[0, 2], [1, 3]]

:p
  (x, y) = (1, (2,3))
  (x, y)
> (1, (2, 3))

:p for i:(Fin 3). i
> [0@(Fin 3), 1@(Fin 3), 2@(Fin 3)]

:p for i:(Fin 3). i
> [0@(Fin 3), 1@(Fin 3), 2@(Fin 3)]

:p fromOrdinal (Fin 1) 0
> 0@(Fin 1)

:p 0@(Fin 1)
> 0@(Fin 1)

:p [1, 2, 3].(0@_)
> 1

:p [0].([asidx 0].(asidx 0))
> 0

:p select True 1 2
> 1

:p select False 1.0 2.0
> 2.0

:p select True [1,2,3] [10,20,30]
> [1, 2, 3]

:p select False [1,2,3] [10,20,30]
> [10, 20, 30]

:p [(1, (for i:(Fin 2) . 3))].(fromOrdinal _ 0)
> (1, [3, 3])

:p (False && False, False && True, True && False, True && True)
> (False, (False, (False, True)))

:p (not False, not True)
> (True, False)

:p (False || False, False || True, True || False, True || True)
> (False, (True, (True, True)))

:p [(for i:(Fin 1). (False, for j:(Fin 2). 1.0)), [(True, for k:(Fin 2) . 2.0)]]
> [[(False, [1.0, 1.0])], [(True, [2.0, 2.0])]]

-- TODO: parse negative integer literals
-- :p (mod 5 3, mod 7 3, mod (-1) 3, mod -5 3)
-- > (2, 1, 2, 1)

:p [[1,2], for i. 3]
> [[1, 2], [3, 3]]

:p select False [1, 2] (for i. 3)
> [3, 3]

:p fold [1, 2] \i:(Fin 3) c. for j. 4
> [4, 4]

:p select True (1,2) (2,3)
> (1, 2)

:p select False (1,[2,3]) (4, for _ . 3)
> (4, [3, 3])

:p select False () ()
> ()

:p [1,2,3].(fromOrdinal (Fin 3) 1 )
> 2

:p fold (1.0, 2.0) \i:(Fin 2) (x, y). (y, x)
> (1.0, 2.0)

:p fold (1.0, 2.0) \i:(Fin 3) (x, y). (y, x)
> (2.0, 1.0)

:p id 2
> 2
:p min 2.0 3.0
> 2.0

:p minBy sq 0.5 (-2.0)
> 0.5

:p minimum [2.0, 3.0, 4.0, 1.5, 7.0]
> 1.5

:p minimumBy fst [(2.0, 20), (1.5, 15), (10.0, 100)]
> (1.5, 15)

:p max 2.0 3.0
> 3.0

:p maxBy sq 0.5 (-2.0)
> -2.0

:p maximum [2.0, 4.0, 1.5, 7.0]
> 7.0

:p maximumBy fst [(2.0, 20), (1.5, 15), (10.0, 100)]
> (10.0, 100)

-- TODO: prefix negation for literals!
:p (1 == 2, (-1) == (-1), 1 < 2, -1 < 2, 2 < (-1))
> (False, (True, (True, (True, False))))

:p (1. == 2., (-1.) == (-1.), 1. < 2., (-1.) < 2., 2. < (-1.))
> (False, (True, (True, (True, False))))

:p for i:(Fin 7). select (i < (fromOrdinal _ 3)) 1 2
> [1, 1, 1, 2, 2, 2, 2]

σ = 1.0 + 2.0

:p σ
> 3.0

δ : Int -> Int = \x. x

:p δ 3
> 3

:p select True 1 2
> 1

:p select True [1,2] [2,3]
> [1, 2]

:p select True (1,2) (2,3)
> (1, 2)

:p
  x = 1; x == x
> True

:p 1 `iadd` 2
> 3

:p slice [1,2,3,4,5] 2 (Fin 3)
> [3, 4, 5]

-- TODO: test file specifically for parse errors (including testing error messages)
:p
   f : Real->Real = \x.
     -- line comment should be ok here
     2.0 * x
   f 1.0
> 2.0

-- Not sure why the ordinary `sum/for` version doesn't work anymore
:p
    n = iadd 3 7
    fsum \i:(Fin n). 1.0
> 10.0

:p
    n = 4
    fsum \i:(Fin n). 1.0
> 4.0

:p
  one = fromOrdinal (Fin 4) 1
  for i:(Fin 4). sum for j:(one..i). 1.0
> [0.0, 1.0, 2.0, 3.0]

:p
  one = fromOrdinal (Fin 4) 1
  for i:(Fin 4). sum for j:(one<..i). 1.0
> [0.0, 0.0, 1.0, 2.0]

:p
  one = fromOrdinal (Fin 4) 1
  for i:(Fin 4). sum for j:(one<..i). 1.0
> [0.0, 0.0, 1.0, 2.0]

:p
  one = fromOrdinal (Fin 4) 1
  for i:(Fin 4). sum for j:(one..<i). 1.0
> [0.0, 0.0, 1.0, 2.0]

:p
  one = fromOrdinal (Fin 4) 1
  for i:(Fin 4). sum for j:(one<..<i). 1.0
> [0.0, 0.0, 0.0, 1.0]

:p
  for i:(Fin 4). sum for j:(..i). 1.0
> [1.0, 2.0, 3.0, 4.0]

:p
  for i:(Fin 4). sum for j:(..<i). 1.0
> [0.0, 1.0, 2.0, 3.0]

:p
  for i:(Fin 4). sum for j:(i..). 1.0
> [4.0, 3.0, 2.0, 1.0]

:p
  for i:(Fin 4). sum for j:(i<..). 1.0
> [3.0, 2.0, 1.0, 0.0]

:p idiv 10 3
> 3

:p rem 10 3
> 1

:p
  xs = [10.,20.]
  ys = [1.,2.,3.]
  xys = for (i,j). xs.i + ys.j
  sum xys
> 102.0

:p
  xs = [10.,20.]
  ys = [1.,2.,3.]
  zs = [1.]
  xys = for (i,(j,k)). xs.i + ys.j + zs.k
  sum xys
> 108.0

:p
  xs = [[1,2],[3,4]]
  ys = for (i,j). xs.i.j
  zs = for (i,j). ys.(i,j)
  for i j. zs.(j,i)
> [[1, 3], [2, 4]]

:p argmin [1.0, 2.0, 0.4, 5.0]
> 2@(Fin 4)

:p select True (fromOrdinal (Fin 2) 0) (fromOrdinal (Fin 2) 1)
> 0@(Fin 2)

-- TODO: printing (also parsing) tables with alternative index sets
-- :p
--   xs = [[1],[2]]
--   for (i,j). xs.i.j
-- > [1, 2]@(2, 1)


-- TODO: injections
-- :p
--   xs = for i:5. 1.0
--   two = fromOrdinal (Fin 5) 2
--   for i:3. sum for j:(two<..). xs.(%inject(j))
-- > [2.0, 2.0, 2.0]

:p
  withState 0.0 \ref. for i:(Fin 4).
      c = get ref
      ref := c + 1.0
      c
> ([0.0, 1.0, 2.0, 3.0], 4.0)

:p
  withState 0.0 \ref. rof i:(Fin 4).
      c = get ref
      ref := c + 1.0
      c
> ([3.0, 2.0, 1.0, 0.0], 4.0)

def eitherFloor (x:Int|Real) : Int = case x
  Left i  -> i
  Right f -> floor f

:p eitherFloor (Right 1.2)
> 1

:p
  x : Int|Real = Right 1.2
  x
> (Right 1.2)

-- Sum types as flattened index sets!
def Weights (n:Type) (m:Type) : Type = n=>m=>Real
def Biases  (n:Type)          : Type = n=>Real
def Params  (n:Type) (m:Type) : Type = ((n&m)|n)=>Real

w = for i:(Fin 2). for j:(Fin 3). (i2r (ordinal i)) * 3.0 + (i2r (ordinal j))
b = for j:(Fin 2). neg (i2r (ordinal j) + 1.0)

def flatten ((w,b):(Weights n m & Biases n)): Params n m =
  for idx. case idx
    Left (i,j) -> w.i.j
    Right i    -> b.i

def unflatten (params:Params n m) : (Weights n m & Biases n) =
  ( for i. for j. params.(Left (i,j))
  , for i.        params.(Right i)    )

:p unflatten (flatten (w, b)) == (w, b)
> True

:p [1,2,3] == [1,2,3]
> True

:p [1,2,4] == [1,2,3]
> False

:p (1, 2) == (1, 2)
> True

:p (for i:(Fin 4). (1.0, ordinal i)) == (for i:(Fin 4). (1.0, ordinal i))
> True

:p (1, 1) == (1, 2)
> False

:p (for i:(Fin 4). 1.0) == (for i:(Fin 4). 2.0)
> False

-- Needed to avoid ambiguous type variables if both sides use the same constructor
def cmpEither (x:Int|Int) (y:Int|Int) : Bool = x == y

:p cmpEither (Left 1) (Left 1)
> True

:p cmpEither (Left 1) (Left 2)
> False

:p cmpEither (Left 1) (Right 1)
> False

-- TODO: within-module version of this (currently fails in Imp checking)
upperBound = isum $ for i:(Fin 4). 1
:p for j:(Fin upperBound). 1.0
> [1.0, 1.0, 1.0, 1.0]

:p (for i:(Fin upperBound). 1, for j:(Fin 2). 2)
> ([1, 1, 1, 1], [2, 2])

for i:(Fin 4).
  x = iadd 1 1
  (x, x)
> [(2, 2), (2, 2), (2, 2), (2, 2)]

:p
  x = 2
  z = for i:(Fin 4).
        y = iadd 2 3
        (x, y)
  (x, z)
> (2, [(2, 5), (2, 5), (2, 5), (2, 5)])

for i:(Range 4 1). 1.0
> []@4...<1

x = isub 2 4
for i:(Range 0 x). 1.0
> []
