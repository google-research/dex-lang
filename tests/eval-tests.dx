:p 1.0 + 2.0
> 3.

:p
   double = \x. x * 2.0
   double 10.0
> 20.

:p sum (iota (Fin 10))
> 45

:p
   x = iota (Fin 10)
   sum (for i. x[i])
> 45

:p
   x = iota (Fin 3)
   prod (for i. x[i] + 1)
> 6

:p
   vdot' : (given (n|Ix),  n=>Float, n=>Float) -> Float =
     \x y. sum (for i. x[i] * y[i] * 2.0)
   x = iota (Fin 3)
   y = map n_to_f x
   vdot' y y
> 10.

:p
   x = iota $ Fin 3
   y = iota $ Fin 4
   z = for i j. n_to_f x[i] * n_to_f y[j]
   sum (for i. sum z[i])
> 18.

:p rand_int (new_key 0)
> 431640317

-- :p let x = unpack range 10000
--        key = hash 0
--    in sum (for i: randint (key x[i]) 10)
NArr = Fin 7

arr = iota NArr

:p for i:NArr. (iota _)[i] + (iota _)[i]
> [0, 2, 4, 6, 8, 10, 12]

:p sum for i:NArr. 1
> 7

fun = \y. sum (map n_to_f arr) + y

:p fun 3.0
> 24.

:p arr
> [0, 1, 2, 3, 4, 5, 6]

:p fold(0) \i x. x + arr[i]
> 21

:p 6.0 - 10.0
> -4.

:p
   f : (given (a:Type, b:Type), ((a)->b,a)) -> b =
     \p.
        (x, y) = p
        x y
   f (\x. x, 1)
> 1

:p
   (x,y) = ((1.0,2.0),3.0)
   (x1, x2) = x
   x1 + x2 + y
> 6.

:p
   x = (1.0,2.0)
   (y,z) = x
   y + z
> 3.

-- :p let f (x, y) = x + 2 * y;
--        z[i] = (x[i], x[i] * x[i])
--    in sum (for i. f z[i])
:p exp 1.0
> 2.718282

:p exp2 3.0
> 8.

:p log 1.0
> 0.

:p log2 8.0
> 3.

:p log10 100.0
> 2.

:p sqrt 2.0
> 1.414214

:p sin 3.14159
> 2.535182e-06

:p cos 0.0
> 1.

:p tan 1.57079
> 159378.3

:p f_to_i $ floor 3.6
> 3

:p f_to_i $ ceil 3.6
> 4

:p f_to_i $ round 3.6
> 4

s = 1.0

:p s
> 1.

:p [2, 4, 8]
> [2, 4, 8]

:p
   xs = [2,1,0]
   for i. xs[i] + 10
> [12, 11, 10]

def cumsumplus(xs:n=>Float) -> n=>Float given (n|Ix) =
  snd $ scan 0.0 \i c.
    ans = c + xs[i]
    (ans, 1.0 + ans)

:p cumsumplus [1.0, 2.0, 3.0]
> [2., 4., 7.]

def cumsum2d(xs:n=>m=>a) -> n=>m=>a given (n|Ix, m|Ix, a|Add) =
  cumsum for i. cumsum xs[i]

xs : (Fin 4)=>(Fin 3)=>Float = arb $ new_key 0
sum (sum xs) ~~ (cumsum2d xs)[3@_,2@_]
> True

:p [False, False, True]
> [False, False, True]

:p [False, True, True, False]  -- testing again because it's been flaky
> [False, True, True, False]

:p (True, False)
> (True, False)

:p 1.0 > 2.0
> False

:p 1.0 > neg 2.0
> True

:p 1.0 < 2.0
> True

:p b_to_i (1.0 < 2.0)
> 1

:p [ 1 < 2, 1.0 < 2.0,  2 < 2, 2.0 < 2.0, 2 < 1, 2.0 < 1.0]
> [True, True, False, False, False, False]

:p [ 1 > 2, 1.0 > 2.0,  2 > 2, 2.0 > 2.0, 2 > 1, 2.0 > 1.0]
> [False, False, False, False, True, True]

:p [ 1 <= 2, 1.0 <= 2.0,  2 <= 2, 2.0 <= 2.0, 2 < 1, 2.0 < 1.0]
> [True, True, True, True, False, False]

:p [ 1 >= 2, 1.0 >= 2.0,  2 >= 2, 2.0 >= 2.0, 2 >= 1, 2.0 >= 1.0]
> [False, False, True, True, True, True]

infty = 1./0.
:p infty
> inf

:p nan
> nan

:p [ 0. < infty, 0. < nan, infty < 0., nan < 0., nan < nan, infty < infty ]
> [True, False, False, False, False, False]

N3 = Fin 3
N4 = Fin 4

mat = for i:N3 j:N4. (iota _)[i] + (10 * (iota _)[j])

-- :p sum for (i,j). mat[i][j]
-- > 192

litArr = [10, 5, 3]

:p mod 5 3
> 2

:p size (Fin 3)
> 3

:p for i:(Fin 3). 1
> [1, 1, 1]

:p
   (k, _) = (1 + 2, 3)
   for i:(Fin 6). k + ordinal i
> [3, 4, 5, 6, 7, 8]

:p
   k = new_key 0
   mean for i:(Fin 100). randn (ixkey k i)
> 0.001849644

:p hash (i_to_w64 0) 0
> 0x6b20015999ba4efe

:p hash (i_to_w64 123) 456
> 0x500945c565c98701

key = (w32_to_w64 (i_to_w32 320440878) .<<. 32) .|. (w32_to_w64 (i_to_w32 57701188)) -- 0x13198a2e03707344
count = (w32_to_w64 (i_to_w32 608135816) .<<. 32) .|. (w32_to_w64 (i_to_w32 2242054355)) -- 0x243f6a8885a308d3
:p threefry_2x32 key count
> 0xc4923a9c483df7a0

:p
   k = new_key 0
   mean for i:(Fin 100). sq $ randn (ixkey k i)
> 0.7444913

:p for i:(Fin 3) j:(Fin 2). rand $ ixkey (new_key 11) (i, j)
> [[0.1513238, 0.07557154], [0.1075971, 0.2888795], [0.5509554, 0.56872]]

:p
  x = for i:(Fin 3). 0
  x
> [0, 0, 0]

:p
   x = for i:(Fin 3). for j:(Fin 4). 0
   x
> [[0, 0, 0, 0], [0, 0, 0, 0], [0, 0, 0, 0]]

:p fold (for i:(Fin 3). 0.0) $ \i:(Fin 2) c. (for j. c[j] + n_to_f (ordinal j))
> [0., 2., 4.]

:p
   mat2 = for i:(Fin 4) j:(Fin 4) . ordinal i
   tmp = for i. mat2[i][i]
   tmp
> [0, 1, 2, 3]

:p
  x = [[0, 1], [2, 3]]
  xt = for i j. x[j][i]
  xt
> [[0, 2], [1, 3]]

:p
  (x, y) = (1, (2,3))
  (x, y)
> (1, (2, 3))

:p for i:(Fin 3). i
> [0, 1, 2]

:p for i:(Fin 3). i
> [0, 1, 2]

:p from_ordinal(n=Fin 1, 0)
> 0

:p 0@(Fin 1)
> 0

:p 5@(Fin 5, Fin 3)
> (1, 2)

:p ordinal (1@(Fin 5), 2@(Fin 3))
> 5

:p for i:(Fin 3, Fin 5). i
> [(0, 0), (0, 1), (0, 2), (0, 3), (0, 4), (1, 0), (1, 1), (1, 2), (1, 3), (1, 4), (2, 0), (2, 1), (2, 2), (2, 3), (2, 4)]

:p [1, 2, 3][0@_]
> 1

:p [0][[asidx 0][asidx 0]]
> 0

:p select True 1 2
> 1

:p select False 1.0 2.0
> 2.

:p select True [1,2,3] [10,20,30]
> [1, 2, 3]

:p select False [1,2,3] [10,20,30]
> [10, 20, 30]

:p [(1, (for i:(Fin 2) . 3))][from_ordinal 0]
> (1, [3, 3])

:p (False && False, False && True, True && False, True && True)
> (False, False, False, True)

:p (not False, not True)
> (True, False)

:p (False || False, False || True, True || False, True || True)
> (False, True, True, True)

:p [(for i:(Fin 1). (False, for j:(Fin 2). 1.0)), [(True, for k:(Fin 2) . 2.0)]]
> [[(False, [1., 1.])], [(True, [2., 2.])]]

-- TODO: parse negative integer literals
-- :p (mod 5 3, mod 7 3, mod (-1) 3, mod -5 3)
-- > (2, 1, 2, 1)

:p [[1,2], for i. 3]
> [[1, 2], [3, 3]]

:p select False [1, 2] (for i. 3)
> [3, 3]

:p fold [1, 2] \i:(Fin 3) c. for j. 4
> [4, 4]

:p select True (1,2) (2,3)
> (1, 2)

:p select False (1,[2,3]) (4, for _ . 3)
> (4, [3, 3])

:p select False () ()
> ()

:p [1,2,3][from_ordinal(n=Fin 3, 1)]
> 2

:p fold (1.0, 2.0) \i:(Fin 2) p.
  (y, x) = p
  (y, x)
> (1., 2.)

:p fold (1.0, 2.0) \i:(Fin 3) p.
  (x, y) = p
  (y, x)
> (2., 1.)

:p id 2
> 2
:p min(2.0, 3.0)
> 2.

:p min_by(sq, 0.5, -2.0)
> 0.5

:p minimum [2.0, 3.0, 4.0, 1.5, 7.0]
> 1.5

:p minimum_by fst [(2.0, 20), (1.5, 15), (10.0, 100)]
> (1.5, 15)

:p max 2.0 3.0
> 3.

:p max_by(sq, 0.5, -2.0)
> -2.

:p maximum [2.0, 4.0, 1.5, 7.0]
> 7.

:p maximum_by fst [(2.0, 20), (1.5, 15), (10.0, 100)]
> (10., 100)

:p (1 == 2, (-1) == (-1), 1 < 2, -1 < 2, 2 < (-1))
> (False, True, True, True, False)

:p (1. == 2., (-1.) == (-1.), 1. < 2., (-1.) < 2., 2. < (-1.))
> (False, True, True, True, False)

:p for i:(Fin 7). select (i < (from_ordinal 3)) 1 2
> [1, 1, 1, 2, 2, 2, 2]

:p select True 1 2
> 1

:p select True [1,2] [2,3]
> [1, 2]

:p select True (1,2) (2,3)
> (1, 2)

:p
  x = 1; x == x
> True

:p 1 + 2
> 3

:p slice([1,2,3,4,5], 2, Fin 3)
> [3, 4, 5]

-- TODO: test file specifically for parse errors (including testing error messages)
:p
   f : (Float)->Float = \x.
     -- line comment should be ok here
     2.0 * x
   f 1.0
> 2.

-- Not sure why the ordinary `sum/for` version doesn't work anymore
:p
    n = 3 + 7
    fsum for i:(Fin n). 1.0
> 10.

:p
    n = 4
    fsum for i:(Fin n). 1.0
> 4.

:p
  for i:(Fin 4). sum for j:(..i). 1.0
> [1., 2., 3., 4.]

:p
  for i:(Fin 4). sum for j:(..<i). 1.0
> [0., 1., 2., 3.]

:p
  for i:(Fin 4). sum for j:(i..). 1.0
> [4., 3., 2., 1.]

:p
  for i:(Fin 4). sum for j:(i<..). 1.0
> [3., 2., 1., 0.]

:p idiv 10 3
> 3

:p rem 10 3
> 1

:p
  xs = [10.,20.]
  ys = [1.,2.,3.]
  xys = for p.
    (i,j) = p
    xs[i] + ys[j]
  sum xys
> 102.

:p
  xs = [10.,20.]
  ys = [1.,2.,3.]
  zs = [1.]
  xys = for ix.
    (i,(j,k)) = ix
    xs[i] + ys[j] + zs[k]
  sum xys
> 108.

:p
  xs = [[1,2],[3,4]]
  ys = for p.
    (i, j) = p
    xs[i][j]
  zs = for p.
    (i,j) = p
    ys[(i,j)]
  for i j. zs[(j,i)]
> [[1, 3], [2, 4]]

:p argmin [1.0, 2.0, 0.4, 5.0]
> 2

:p select(True, from_ordinal(n=Fin 2, 0), from_ordinal(n=Fin 2, 1))
> 0

-- TODO: printing (also parsing) tables with alternative index sets
-- :p
--   xs = [[1],[2]]
--   for (i,j). xs[i][j]
-- > [1, 2]@(2, 1)


-- TODO: injections
-- :p
--   xs = for i:5. 1.0
--   two = fromOrdinal (Fin 5) 2
--   for i:3. sum for j:(two<..). xs.(%inject(j))
-- > [2.0, 2.0, 2.0]

:p
  run_state 0.0 \ref. for i:(Fin 4).
      c = get ref
      ref := c + 1.0
      c
> ([0., 1., 2., 3.], 4.)

:p
  run_state 0.0 \ref. rof i:(Fin 4).
      c = get ref
      ref := c + 1.0
      c
> ([3., 2., 1., 0.], 4.)

def eitherFloor(x:(Int `Either` Float)) -> Int = case x of
  Left i  -> i
  Right f -> f_to_i $ floor f

:p eitherFloor (Right 1.2)
> 1

:p
  x : (Either Int Float) = Right 1.2
  x
> (Right 1.2)

:p [1,2,3] == [1,2,3]
> True

:p [1,2,4] == [1,2,3]
> False

:p (1, 2) == (1, 2)
> True

:p (for i:(Fin 4). (1.0, ordinal i)) == (for i:(Fin 4). (1.0, ordinal i))
> True

:p (1, 1) == (1, 2)
> False

:p (for i:(Fin 4). 1.0) == (for i:(Fin 4). 2.0)
> False

((Left 1) :: (Int `Either` Bool)) == (Left 1 :: (Int `Either` Bool))
> True

((Right False) :: (Int `Either` Bool)) == (Left 1 :: (Int `Either` Bool))
> False

((Right False) :: (Bool `Either` Bool)) == (Left False :: (Bool `Either` Bool))
> False

Just 1 == Just 1
> True

Just 1 == Just 0
> False


-- Userspace-Bool breaks these

-- -- Needed to avoid ambiguous type variables if both sides use the same constructor
-- def cmpEither(x:Int|Int) (y:Int|Int) : Bool = x == y

-- :p cmpEither (Left 1) (Left 1)
-- > True

-- :p cmpEither (Left 1) (Left 2)
-- > False

-- :p cmpEither (Left 1) (Right 1)
-- > False

-- TODO: within-module version of this (currently fails in Imp checking)
upperBound = sum $ for i:(Fin 4). (1::Nat)
:p for j:(Fin upperBound). 1.0
> [1., 1., 1., 1.]

:p (for i:(Fin upperBound). 1, for j:(Fin 2). 2)
> ([1, 1, 1, 1], [2, 2])

for i:(Fin 4).
  x = 1 + 1
  (x, x)
> [(2, 2), (2, 2), (2, 2), (2, 2)]

:p
  x = 2
  z = for i:(Fin 4).
        y = 2 + 3
        (x, y)
  (x, z)
> (2, [(2, 5), (2, 5), (2, 5), (2, 5)])

-- TODO: re-enable these once we prevent (`(-3):Nat` occurring
for i:(Fin (-3)). 1.0
> Type error:Can't reduce type expression: (Fin -3 [])
>
> for i:(Fin (-3)). 1.0
>       ^^^^^^^^^^

x = 2 - 4
> Type error:Couldn't synthesize a class dictionary for: (Sub Nat)
>
> x = 2 - 4
>     ^^^^^

-- Make sure that we can construct and print an array using a pair index set
for i:(Fin 2, Fin 2). 1.0
> [1., 1., 1., 1.]

1@(Fin 2, Fin 2)
> (0, 1)

for i:(Fin 5). for j:(..i).
  ir = n_to_f $ ordinal i
  jr = n_to_f $ ordinal j
  ir * (ir + 1.0) / 2.0 + jr
> [[0.], [1., 2.], [3., 4., 5.], [6., 7., 8., 9.], [10., 11., 12., 13., 14.]]

-- TODO: fix!
-- -- Exercise the use of free variables in the sum solver
-- :p
--   n = 2 + 2
--   x = for i:(Fin n). for j:(..i). 1.0
--   sum $ for i. sum x.i
-- > 10.0


:p 1 + 2
> 3

:p [+1,2] - [2,10]
> [-1, -8]

:p 2.0 .* [[1.0, 2.0], [3.0, 4.0]]
> [[2., 4.], [6., 8.]]

def newtonIter(f: (Float)->Float, x:Float) -> Float =
  x - (f x / deriv f x)

def newtonSolve(tol:Float, f: (Float)->Float, x0:Float) -> Float =
  yield_state x0 \x.
    iter \i.
      if abs (f $ get x) <= tol
        then Done ()
        else
          x := newtonIter f $ get x
          Continue

-- :p newtonSolve 0.001 (\x. sq x - 2.0) 1.0
-- > 1.414216

-- :p
--   x = for i:(Fin 3). for j:(Fin 200). 1.0
--   -- Last dimension split to allow for vector loads
--   y = for i:(Fin 200). for j:(Fin 4). for h:(Fin VectorWidth). IToF $ (iota _).(i,j,h)
--   z = yield_accum \acc.
--         for l.
--           for i.
--             xil = (broadcastVector x.i.l)
--             for j.
--               acc!i!j += xil * (loadVector y.l[j])
--   for i:(Fin 3). for j:(Fin 4). storeVector z.i[j]
-- > [ [ [318400.0, 318600.0, 318800.0, 319000.0]
-- > , [319200.0, 319400.0, 319600.0, 319800.0]
-- > , [320000.0, 320200.0, 320400.0, 320600.0]
-- > , [320800.0, 321000.0, 321200.0, 321400.0] ]
-- > , [ [318400.0, 318600.0, 318800.0, 319000.0]
-- > , [319200.0, 319400.0, 319600.0, 319800.0]
-- > , [320000.0, 320200.0, 320400.0, 320600.0]
-- > , [320800.0, 321000.0, 321200.0, 321400.0] ]
-- > , [ [318400.0, 318600.0, 318800.0, 319000.0]
-- > , [319200.0, 319400.0, 319600.0, 319800.0]
-- > , [320000.0, 320200.0, 320400.0, 320600.0]
-- > , [320800.0, 321000.0, 321200.0, 321400.0] ] ]

:p
  fs = for i:(Fin 4).
    x = ordinal i
    \y. x + y
  for i. fs[i] (ordinal i)
> [0, 2, 4, 6]

:p
  f = with_state 1 \ref.
    x = get ref
    ref := 3 + x
    y = get ref
    \z. z + 10 * x + 100 * y
  f 5
> 415

:p
  (f, w) = run_accum (AddMonoid Float) \ref.
    ref += 2.0
    w = 2
    \z. z + w
  (f 5, w)
> (7, 2.)

-- def add(n : Type) ?-> (a : n=>Float) (b : n=>Float) : n=>Float =
--   (tile (\t:(Tile n (Fin VectorWidth)). storeVector $ loadTile t a + loadTile t b)
--         (\i:n. a.i + b.i))
-- toAdd = for _:(Fin 10). 1.0
-- add toAdd toAdd
-- > [2.0, 2.0, 2.0, 2.0, 2.0, 2.0, 2.0, 2.0, 2.0, 2.0]

arr2d = for i:(Fin 2). for j:(Fin 2). (iota _)[(i,j)]
arr2d[1@_]
> [2, 3]

:p any [True, False]
> True
:p any [False, False]
> False
:p any []
> False

:p all [True, True]
> True
:p all [False, True]
> False
:p all []
> True


'Tests for numerical utilities

easy = [(-2.0), 3.0, 3.0, 0.1, 0.0]
hard = [(-1000.0), 1000.0, 1000.0, 0.1, 0.0]

:p logsumexp easy - (log $ sum for j. exp easy[j])
> 0.

:p sum $ softmax hard
> 1.

:p all for i. ((softmax hard)[i] >= 0.0)
> True

:p sum for i. exp $ (logsoftmax hard)[i]
> 0.9999709

:p all for i. abs ((softmax hard)[i] - exp (logsoftmax hard)[i]) < 0.0001
> True

:p evalpoly [2.0, 3.0, 4.0] 10.0
> 234.

str = ['x', 'y']

:p str
> [0x78, 0x79]

s1 = "hello world"

:p s1
> "hello world"

:p codepoint 'a'
> 97

:p 'a' == 'a'
> True

:p 'a' == 'A'
> False

:p 'a' < 'b'
> True

:p 'a' > 'b'
> False

:p
  x = 2 + 2
  y = 2 + 4
  f = \z. case z == 4 of
            True  -> (\w. w + 2)
            False -> (\w. w)
  (f(x)(4), f(y)(4))
> (6, 4)

:p
  nz = sum [1, 1, 2]
  zs = for i:(Fin nz).
    i' = ordinal i
    i'

  sum zs + sum zs
> 12

-- triLit : (i:Fin 4)=>(..i)=>Int =
--   [ coerce_table _ [0]
--   , coerce_table _ [1, 2]
--   , coerce_table _ [3, 4, 5]
--   , coerce_table _ [6, 7, 8, 9] ]
-- triLit
-- > [[0], [1, 2], [3, 4, 5], [6, 7, 8, 9]]

-- @noinline
def fromLeftFloat(x:(Float `Either` Int)) -> Float =
  case x of
    Left x' -> x'
    Right _ -> error "this is an error"

:p fromLeftFloat $ Right 1
> this is an error
> Runtime error

:p fromLeftFloat $ Left 1.2
> 1.2

@noinline
def f1(x:Int) -> Int = x + 1

@noinline
def f2(x:Int) -> Int = f1 $ f1 $ f1 $ f1 $ f1 $ f1 $ f1 $ f1 $ f1 $ f1 $ x

@noinline
def f3(x:Int) -> Int = f2 $ f2 $ f2 $ f2 $ f2 $ f2 $ f2 $ f2 $ f2 $ f2 $ x

@noinline
def f4(x:Int) -> Int = f3 $ f3 $ f3 $ f3 $ f3 $ f3 $ f3 $ f3 $ f3 $ f3 $ x

@noinline
def f5(x:Int) -> Int = f4 $ f4 $ f4 $ f4 $ f4 $ f4 $ f4 $ f4 $ f4 $ f4 $ x

@noinline
def f6(x:Int) -> Int = f5 $ f5 $ f5 $ f5 $ f5 $ f5 $ f5 $ f5 $ f5 $ f5 $ x


-- regression test for #1229 - checks that constraints and data args can be interleaved
@noinline
def interleave_args(x:Float, xs:n=>Float) -> Float given (n|Ix) =
  x + sum xs

interleave_args(1.0, [2.0, 3.0])
> 6.


-- This will compile extremely slowly if non-inlining is broken
:p f6 0
> 100000

-- Testing integer powers

:p intpow 10 2
> 100
:p intpow 0 0
> 1
:p intpow 0 1
> 0
:p intpow 1 0
> 1
:p intpow 1 1
> 1
:p intpow 2 1
> 2
:p intpow 2 3
> 8
:p intpow 2 17
> 131072

def f(x:Float) -> Float =
  intpow x 2

x2 = 3.1
f x2
> 9.61

grad f x2
> 6.2

-- Integer utility tests

:p map is_power_of_2 [0, 1, 2, 3, 256, 1023, 1024, 1024*1024, 1024*1024 + 1]
> [False, True, True, False, True, False, True, True, False]

:p map natlog2 [1, 2, 3, 4, 5, 1023, 1024, 1025]
> [0, 1, 1, 2, 2, 9, 10, 10]

interface AssociatedInt(a:Type)
  value : () -> Int

instance AssociatedInt(Int)
  def value() = 2

instance AssociatedInt(Float)
  def value() = 4

value(a=Int)
> 2
value(a=Float)
> 4

interface AssociatedWithTwo(a:Type, c:Type)
  value2 : () -> Int

instance AssociatedWithTwo(Int, Float)
  def value2() = 8

-- TODO: the source location for this error message is a bit off since we added
-- n-ary applications.
value2(a=Int, c=Float)
> 8
value2(a=Float, c=Int)
> Type error:Couldn't synthesize a class dictionary for: (AssociatedWithTwo Float32 Int32)
>
> value2(a=Float, c=Int)
> ^^^^^^^^^^^^^^^^^^^^^^

-- XXX: Disabled because we now compile expressions like these to blocks, which
-- need a type that makes sense in the top scope. We may revisit that choice.
-- -- checks that mixing non-data values into the results of case expressions don't
-- -- prevent them from later being used in types.
-- :p
--   x = 1 + 1
--   (n, _) = case x > 1 of
--     True  -> (x, \x:Int. x)
--     False -> (x, \x:Int. x)
--   for i:(Fin n). 1.0
-- > [1., 1.]

1.0e-4
> 0.0001

-2.0e-2
> -0.02

4.0e4
> 40000.

'### Misc. index sets

all for x:(Bool, Bool).
  x == (unsafe_from_ordinal (ordinal x))
> True

all for x:((Bool, Bool)=>(Fin 3)).
  x == (unsafe_from_ordinal (ordinal x))
> True

all for x:((Bool, Fin 3)=>(Fin 4)).
  x == (unsafe_from_ordinal (ordinal x))
> True

all $ for i:(Maybe (Fin 2)).
  i == (unsafe_from_ordinal (ordinal i))
> True

def all_prefixes(s:String)  -> List String =
  AsList(_, s') = s
  to_list for i. post_slice s' first_ix i

:p all_prefixes "fence"
> (AsList 6 ["", "f", "fe", "fen", "fenc", "fence"])

-- NonEmpty tests

first_ix :: ()
> ()

first_ix :: (() `Either` Fin 2)
> (Left ())

first_ix :: Bool
> False

first_ix :: (() `Either` Fin 2)=>(() `Either` Fin 3)
> [(Left ()), (Left ()), (Left ())]

first_ix :: ((() `Either` Fin 2), (() `Either` Fin 3))
> ((Left ()), (Left ()))

first_ix :: ((() `Either` Fin 2) `Either` Fin 3)
> (Left (Left ()))


-- Check that empty sets can't satisfy NonEmpty

first_ix :: (Fin 0)
> Type error:Couldn't synthesize a class dictionary for: (NonEmpty (Fin 0))
>
> first_ix :: (Fin 0)
> ^^^^^^^^^

first_ix :: (Fin 0 `Either` Fin 0)
> Type error:Couldn't synthesize a class dictionary for: (NonEmpty (Either (Fin 0) (Fin 0)))
>
> first_ix :: (Fin 0 `Either` Fin 0)
> ^^^^^^^^^

first_ix :: (Fin 0, ())
> Type error:Couldn't synthesize a class dictionary for: (NonEmpty (Fin 0, ()))
>
> first_ix :: (Fin 0, ())
> ^^^^^^^^^


-- Subset tests

project(to=(), inject(to=(() `Either` Fin 2), ()))
> (Just ())

project(to=(), inject(to=(() `Either` Fin 2), 0@(Fin 2)))
> Nothing

unsafe_project(to=(), inject(to=() `Either` Fin 2, ()))
> ()

unsafe_project(to=(), inject(to=(Fin 2 `Either` ()), ()))
> ()

:p unsafe_coerce(to=Fin 1 => (Nat, Nat), ([1], [2]))
> [(1, 2)]

-- Stack tests

:p
  with_stack Float \stack.
    stack.push(1.0)
    stack.push(2.0)
    stack.extend([3.0, 4.0])
    stack.extend(for i:(Fin 10). n_to_f $ ordinal i)
    stack.read()
> (AsList 14 [1., 2., 3., 4., 0., 1., 2., 3., 4., 5., 6., 7., 8., 9.])

:p
  with_stack (Float, (Fin 2=>Float)) \stack.
    stack.push((1.0, [1.0, 2.0]))
    stack.extend([(10.0, [10.0, 20.0]), (100.0, [100.0, 200.0])])
    stack.read()
> (AsList 3 [(1., [1., 2.]), (10., [10., 20.]), (100., [100., 200.])])

-- Checks for O(N^2) behavior
:p
  n = 1000000
  with_stack Float \stack.
    for i:(Fin n).
      stack.push(2.0)
      stack.extend([3.0, 4.0])
    AsList(_, xs) = stack.read()
    sum xs
> 9000000.

-- Non-data case scrutinee tests

-- Check that we can print out a sum-typed non-data value
Just f64_to_f
> (Just <function of type ((x:Float64) -> Float32)>)

-- Check that we can print out a sum-typed non-data value that came
-- from a case (this tickles ACase colliding with the code generated
-- to print).  The @noinline defeats case-of-known-constructor.
@noinline
def id_bool(x:Bool) -> Bool = x

case (id_bool True) of
  True -> Just f64_to_f
  False -> Nothing
> (Just <function of type ((x:Float64) -> Float32)>)

-- Check that we can later scrutinize a sum-typed non-data value that
-- came from a case.
foo = case (id_bool True) of
  True -> Just f64_to_f
  False -> Nothing

case foo of
  Just _ -> 1.0
  Nothing -> 2.0
> 1.

-- Non-data catch tests

-- Should be able to catch a function
-- This is a regression test for Issue #1193.
catch \. f64_to_f
> (Just <function of type ((x:Float64) -> Float32)>)

-- Should be able to catch a function-valued computation that fails
catch \.
  x = if True
    then throw()
    else 1.0
  \y. x + y
> Nothing

-- Should be able to apply a function that came out of a catch
frob : (Maybe ((Float) -> Float)) = catch \. \x. x + x

case frob of
  Just func -> func 2.0
  Nothing -> 0.0
> 4.


-- regression tests for #1212
data Rectangle(a) = AsRectangle(n:Nat, m:Nat, elts:(Fin n => Fin m => a))
data Brick(a) = AsBrick(n:Nat, m:Nat, l:Nat, elts:(Fin n => Fin m => Fin l => a))

def mk_rect(n:Nat) -> Rectangle Nat =
  AsRectangle _ _ $ for i:(Fin n) j:(Fin n). ordinal i

def mk_brick(n:Nat) -> Brick Nat =
  AsBrick _ _ _ $ for i:(Fin n) j:(Fin n) k:(Fin n). ordinal i

rect = mk_rect(3)
rect
> (AsRectangle 3 3 [[0, 0, 0], [1, 1, 1], [2, 2, 2]])

brick = mk_brick(3)
brick
> (AsBrick 3 3 3 [[[0, 0, 0], [0, 0, 0], [0, 0, 0]], [[1, 1, 1], [1, 1, 1], [1, 1, 1]], [[2, 2, 2], [2, 2, 2], [2, 2, 2]]])
